
-> start

=== start

Schwarze Pappeln am Wegrand.

Am Himmel ein fahler, dreiäugiger Mond.

Und das unablässige Trommeln der Tropfen auf dem Regenschirm. 


"Wir sind gleich da", sagt Marek mit monotoner Stimme, "Nur noch ein kleines Stück."

    * Du denkst: "Vielleicht gehe ich besser nach Hause."
    
        Vielleicht wäre es besser, umzukehren, ja. Aber du gehst weiter.

    * Du sagst: "Vielleicht gehe ich besser nach Hause."

        "Keine Sorge", antwortet Marek roboterhaft, "Gleich da."
        
        ** "Wohin gehen wir?"
        
            "So etwas hast du noch nicht gesehen. Vertrau mir. Das haut dich um."
        
        ** "Meine Füße sind schon ganz nass."
        
            "Eben", sagt er, "drinnen ist es warm."
            
        --
        Du seufzt und murmelst ein kleines "Okay". Dann geht ihr weiter.

    * Du gehst schweigend weiter.
        Schweigend geht ihr weiter.

-

Der Pfad macht einen Schlenker. Die Pappeln ducken sich weg und geben den Blick auf die schwarze Silhouette einer Villa frei ... eher ein Palast als eine Villa. Und kein einziges Fenster ist erleuchtet.

* "Gehört das alles hier dir?"

    "Hmmm", macht er nur.
    
* "Der Film hat mir wirklich gut gefallen."
    
    "Der Film?", fragt Marek geistesabwesend. "Oh ja, <i>der</i> Film."
    
* "Das Haus da ist ein bisschen unheimlich."
    
    "Unheimlich?", antwortet Marek, "Unheimlich <i>groß</i>."

    ** "Und du wohnst hier?"
        "Hmm ...", macht Marek enigmatisch.
    
    ** "Wer wohnt hier?"
        "Hmm ...", macht Marek enigmatisch.


-


Ihr erreicht eine große, schwarze Pfütze, in der der Regen tanzt. Sie sieht aus wie ein enormer Schlund im Boden, tiefer als der Marianengraben.

Den Bruchteil einer Sekunde lang will Marek nach links und du nach rechts ausweichen, der Regenschirm, den er über eure Köpfe hält, kippt auf seine Seite und ein kalter Schwall Wasser trifft deinen Kopf.


* "Fuck!"
    "Hier lang", sagt Marek nur.
    
* "Upps! Mein Fehler."
    "Mmmh", macht Marek bekräftigend.

* "Wow. Ein echter Gentleman."
    "Nicht mein Fehler", sagt Marek ernst.
    
    ** "DOCH dein Fehler."
    
        --- (verdammt_pass_auf)
        Er räuspert sich. "Hinterher wirst du dich bedanken, dass ich dich mitgenommen habe", sagt er. Er klingt etwas gereizt.

        *** "Was soll das bitte heißen? Soll ich mich bei dir bedanken, wenn ich nass werde?"
            "Nein", antwortet er kühl, "Lass uns einfach weitergehen, okay?"
        
        *** "Es war dein Fehler. Punkt."
            "Okay", sagt er kühl.
        
        *** "Ich bin schon gespannt."
            "Okay", antwortet er ohne Begeisterung.
        
    ** "War nur ein Witz."
        "Klar", antwortet er teilnahmslos.

* "Verdammt, pass doch auf!"
    -> verdammt_pass_auf

-

Ihr umrundet die Pfütze und steuert weiter auf das Gebäude zu. Endlich erreicht ihr den Eingang.

Marek sperrt auf.

"Ladies first", sagt er und deutet auf die Tür.

* Trete ein.

* Sage: "Du zuerst!"
    Er lacht unvermittelt: "Na, meinetwegen."
    
    Er dreht sich um und tritt ohne ein weiteres Wort ein.
    
    ** Folge ihm
    
    ** Gehe nach Hause
        Du zögerst. Du fühlst dich nicht ganz wohl bei der Sache. Sollst du nach Hause gehen?
        
        Hinter dem Vordach, unter dem du stehst, fällt der Regen in Strömen herab. Du willst nicht klatschnass werden.
        
        *** Du denkst: "Vielleicht gehe ich nur kurz rein und bitte ihn, mir seinen Regenschirm zu leihen."
        
        Ja, das klingt nach einem guten Plan.
        
        *** Du denkst: "Vielleicht gehe ich nur kurz rein und bitte ihn, mir ein Taxi zu rufen."
        
        Ja, das klingt nach einem guten Plan.
        
        *** Du denkst: "Was soll's, ich bleib doch hier!"
        
        Jetzt wo du schon so weit gekommen bist, kannst du auch hier bleiben.
    
-

Du trittst ein.

-> foyer

=== foyer


- (backfoyer)
Du findest dich in einem großen Eingangssaal mit Marmorwänden wieder. Bis auf eine ((@bueste Marmorbüste)) und ein halbes Dutzend antiker ((@foyer_gemaelde Gemälde)) an den Wänden ist er vollständig leer. 

Marek ist nirgendwo zu sehen.


* @bueste betrachte die Marmorbüste
    Antonín Dvořák. Nicht, dass du sein Gesicht erkannt hättest; der Name steht auf einem Schild unter der Büste.

* @bueste berühre sie
    Kalter Marmor.

* @bueste nimm sie mit
    Du versuchst allen Ernstes, sie anzuheben, aber natürlich ist sie zu schwer.

* @foyer_gemaelde Betrachte die Gemälde
    Dein Blick schweift über die Bilder, aber du hast nicht die Muße, sie aufmerksam zu betrachten.

-

-> backfoyer

-> END






