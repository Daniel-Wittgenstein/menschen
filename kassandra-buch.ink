
VAR buch_wand = false

=== k_buch

Es ist eine schwarz-rote DIN-A5-Chinakladde. Du schlägst sie auf und beginnst zu lesen.

* weiter
-

"Liebe Kassandra", steht auf der ersten Seite.

* weiter
-

... die restlichen Seiten sind alle leer. Na, wundervoll.

Und du dachtest schon, du bekommst endlich Antworten, was diese ganze verrückte Geschichte mit den unsichtbaren Menschen angeht.

+ Schmeiße das Buch vor Frust an die Wand
    ~ buch_wand = true
    
    "Sachte! Sonst kriegt die Krankenschwester noch mit, dass ich hier bin."

+ Stöhne laut

    "Pssst! Sonst kriegt die Krankenschwester noch mit, dass ich hier bin."

Du zuckst zusammen. Es ist Kross' Stimme. Und sein Geruch. Unverkennbar.

-

+ "Ich hatte gehofft, dich nie wieder zu sehen."

    "Nun, <i>sehen</i> wirst du mich nicht müssen", sagt er. 

+ "Kross?"

    "Genau der."

+ "Wenn du gekommen bist, um Kassandra etwas zu tun ..."

    Er lacht trocken. Nach ein, zwei Takten geht sein Lachen in Husten über. "Ich würde Cassie nichts tun."

-
Du siehst wie der Bezug des Stuhls neben dir von seinem Gewicht heruntergedrückt wird, als er sich neben dich setzt.

+ "Was machst du hier?"
    "Ich wollte Cassie sehen."

+ "Was willst du von mir?"

    "Nichts. Ich bin nur gekommen, um Cassie zu sehen."

-
Er macht eine Pause. "Es war ein Fehler, den Dolch zu nehmen. Aber Penelope wollte ihn mir unbedingt schenken."

+ "Wer ist Penelope?
    "Du hast sie doch getroffen", sagt er, "Hat sie sich nicht vorgestellt?"

+ "Ich habe sie getroffen."
    "Ich weiß. Sie hat es mir erzählt. Deswegen bin ich ja hier."

-
Du hörst sein Atmen neben deinem Ohr. "Penelope ist ganz schön durch den Wind, oder? Früher nicht. Früher war sie klüger als wir alle."

+ "Ja."

    "Hmmm", macht Kross nur und hustet. Du fragst dich, ob er die Hand vor seinen Mund hält, oder ob das eine Sache ist, die man sein lässt, wenn einen sowieso niemand sieht. <>

+ "Ich wäre fast gestorben."

    "Diese verfickten Fallen", murmelt er. <>
    
-
"Ich hätte den Dolch nie an Marek verkaufen sollen. Und dann, ich meine, nach der Sache ... mit dir und so", er pausiert, "da dachte ich mir: dieser Scheißdolch, schaff ihn endlich weg, also habe ich ihn zurück zu Penelope gebracht."

* "Und dann hast mich zu ihr geschickt, damit ich sterbe?"

    "Was? Nein! Ich wusste nicht, dass sie das ganze Haus mit Scheißfallen zugebaut hat. Sie war früher nicht verrückt. Nicht <i>so</i> verrückt jedenfalls. Ich wollte nicht, dass dir etwas passiert. Oder ... Cassie."

* "Sehr klug von dir."

    "Hör mal, ich wusste nicht, dass sie das ganze Haus mit Fallen zugebaut hat. Sonst hätte ich dich nicht da einfach so hingehen lassen. Ich wollte nicht, dass dir etwas passiert. Oder ... Cassie."
    
-

* "Ich nehme an, du willst mir nicht endlich erzählen, was es mit dieser ganzen Geschichte auf sich hat?"

* "Woher kennst du Kassandra?"


-
Er seufzt. "Scheiß drauf. Dann erzähle ich es eben. Nichts, worauf man Stolz sein sollte. Deswegen wollte ich nie darüber reden. Aber früher oder später findest du es sowieso raus. Scheint ja ein Talent von dir zu sein."

* "Okay."

* "Erzähl mir nicht, was meine Talente sind."

    "Du hast ein ganz schönes Temperament, weißt du?"

    ** "Ich wäre fast gestorben! Mehrmals!"
    
        "Ja", brummt er, "An deiner Stelle wäre ich auch wütend. Wahrscheinlich. Wenn ich jünger wäre. Aber ich glaube nicht mehr an Entschuldigungen."
    
    ** "Fick dich!"
    
        "Hmmm...", brummt er, "An deiner Stelle wäre ich auch wütend. Wahrscheinlich. Wenn ich jünger wäre. Aber auf Dauer hilft die Wut auch nicht weiter."

    
    ** "Echt?"
        "Hmmm", brummt er, "Zumindest mein Eindruck."

-
{buch_wand:
    Er räuspert sich laut. "Ich schätze, du hast das Buch an die Wand geworfen, weil nichts drin stand, oder? Penelope wollte alles aufschreiben. Schon seit langer Zeit. Ist ihr wohl genauso schwer gefallen wie mir."
-else:
    Die Seiten des Buchs, das du in der Hand hältst, werden umgeblättert. "Steht immer noch nichts drin. Dachte ich mir. Penelope wollte alles aufschreiben. Schon seit langer Zeit. Ist ihr wohl genauso schwer gefallen wie mir."
}


+ weiter

Kross schweigt. Eine Krankenschwester läuft an euch vorbei. Hinter ihr weht der Geruch von Desinfektionsmittel  her. Die Krankenschwester verschwindet in einem Aufzug und ihr seid wieder alleine.

"Wir haben jemanden umgebracht, Hannah", sagt Kross unvermittelt, "Wir waren jung. Gerade erst achtzehn geworden. Ja. Das ist die ganze Geschichte."
-


+ "Wie - die ganze Geschichte? Kommt da nicht noch mehr?"

+ "Wer ist WIR?"


-
Er seufzt. "Benson kam aus England. Penelope aus Belgien. Niemand wusste, ob sie wirklich zusammen sind oder nur platonisch befreundet. Wir haben uns in Italien im Urlaub kennengelernt. Das war in den Neunzigern, der Kommunismus war noch nicht lange vorbei. Ich bin aus Prag rüber nach Italien, ich war jung und bescheuert, ich lerne diese Typen kennen, ich denke, ich habe bei Penelope ne Chance. Wenig später ist Jirka dazugekommen. Du kennst ihn vielleicht aus der Zeitung."

+ "Wer?"

+ "Häh?"

-

"Jirka Němec."

+ "Der Präsidentschaftskandidat?"

    "Genau der", sagt Kross. <>

+ "Jetzt weiß ich, wieso mir das Foto bekannt vorkam!"

    "Hmmm ...", macht Kross. <>

-

"Er hat mit der Sache angefangen. Mit der schwarzen Magie. Benson hat so halb dran geglaubt. Penny und ich haben drüber gelacht. Wir wollten sie nicht töten. Nur ... wir waren betrunken und wollten ihr Blut für dieses ... bescheuerte Ritual. Eine einzige Scheiße. Jirka hatte diesen Ramschdolch irgendeinem Marokkaner am Strand abgekauft und dachte wirklich, also der Marokkaner, oder vielleicht war es ein Tunesier, ich erinnere mich schon gar nicht mehr, meinte, dass der Dolch magische Kräfte besitzt. Und Jirka hatte von so einem Ritual gelesen oder der Marokkaner hatte ihm davon erzählt und er hatte sich dann ein Buch geholt und selber noch was dazu gelesen, ich weiß nicht mehr, das war ein Ritual, irgendwas mit einer wird sichtbar, wenn alle nicht mehr sichtbar sind. Und wir haben natürlich Null verstanden, was das bedeuten soll, weil ja diese Prophezeiungssachen immer so sind, dass man nicht versteht, was damit gemeint ist, und wir waren alle Jirka am Verarschen, weil er als einziger wirklich dran geglaubt hat. Er war ja der einzige von uns mit Abitur und kam sich megaintelligent vor und für uns war es lustig, dass er trotzdem so einen Scheiß glaubt. Und das hat sich so hochgeschaukelt, weil wir alle sehen wollten, wie weit er geht. Dann haben wir sie gefesselt ..."

+ "Wer ist SIE?"

    "Was? Irgendwer. Ein Mädchen. Sie war ein Jahr jünger als wir, oder so. 

+ Schweige.
    "<>
    
-
Wir haben sie gefesselt und den Mund zugebunden, damit die anderen im Hostel nichts hören und ..."
    
+ "Sie getötet?"

    "Nein! Wir wollten ihr nur Angst machen mit dem Dolch und haben an ihr ein bisschen rumgeritzt."

+ Schweige.

    ... wir wollten ihr ein bisschen Angst machen mit dem Dolch und haben an ihr ein bisschen rumgeritzt."
    
-

+ "Ihr habt WAS?!"

    "Es war nur als Scherz gemeint. Es war ... es war eine abgefuckte Scheiße, ja. Und dann hat Jirka

+ Schweige.

    "Und Jirka hat
    
-

<> diese arabischen oder lateinischen oder was auch immer Beschwörungsformeln gesprochen und dann hat er gesagt, dass es jetzt begonnen hat und dass wir es zu enden führen müssen und dass wir sie töten müssen. Dann ist es total ausgeartet. Benson hat versucht, ihm den Dolch wegzunehmen, und Penelope ist auf einmal total paranoid geworden und hat geschrien, hier ist was im Zimmer, hier ist was im Zimmer, merkt ihr das nicht, und ich wollte nicht, dass Benson irgendeine Scheiße macht und Jirka absticht, also hab ich ihm den Dolch weggerissen und ...", er atmet schwer, "Ich war besoffen, okay? Ich wollte irgendwie ... einen Spaß machen oder dem ganzen eine witzige Wendung geben, keine Ahnung, damit die anderen aufhören zu streiten, dann hab ich dem Mädchen, hab ihr das Höschen aufgeschnitten."

+ "WAS?!"

+ "Was soll daran witzig sein?!"

+ Schweige.

-

"Ich wollte ihn ablenken. Ich dachte, wenn er an Sex denkt, hört er auf mit der Scheiße."

+ "Du bist dermaßen ekelhaft."

    Er seufzt: "Ich war jung."

    ++ "Soll das eine Ausrede sein?"
        "Nein. Aber ich habe sie nicht getötet.
    
    ++ Schweige.
        "<>Und dann ...

+ "Das arme Mädchen."
    "Ich habe sie nicht getötet. 

+ "Und dann?"
    "<>

+ Schweige.
    "<>

-
<> Jirka hat meinen Arm ... ER hat auf meinen Arm geschlagen und der Dolch ist ... in sie rein. Und sie war ja schon am Verbluten, also mussten wir sie ..."

+ "... ganz umbringen."
    Er hustet nur, wie um <i>ja<i> zu sagen.

+ Schweige.
    Er seufzt.

-

"Hast du nicht beim letzten Mal, als wir uns gesehen haben, gesagt, dass ich ein Monster bin? Jetzt hast du die Bestätigung."

+ "Erwartest du jetzt von mir so etwas wie Vergebung?"
    -> k_kross_vergebung
    
+ "Ich habe Arschloch gesagt, nicht Monster."

    "So ist das also." Er lacht trocken. "Tja. Ich schätze, von dir darf ich also kein Verstädnis erwarten."
    -> k_kross_verstaendnis
    
+ "Mir ist es egal, was du bist. Ich will nur wissen, wie die Geschichte weiter geht."

    "Du bist echt ein Phänomen." Er lacht kurz und freudlos. "Hast du eigentlich überhaupt Gefühle oder interessiert es dich immer nur, alles zu wissen?"

    ++ "Letzteres."
        "So ist das also." Er lacht noch einmal trocken. "Tja. Ich schätze, von dir darf ich also kein Verstädnis erwarten."
        -> k_kross_verstaendnis

    ++ "Erwartest du jetzt, dass ich dir vergebe, oder was?"
        -> k_kross_vergebung
   
- 
-> END
        
        
=== k_kross_vergebung

"Was weiß ich." Er flucht leise. "Vielleicht, ja. Aber von dir darf ich wohl kein Verständnis erwarten."

-> k_kross_verstaendnis

=== k_kross_verstaendnis

+ "Nein, darfst du nicht."

    -> kross_kein_mitleid

+ "Faszinierend, wie du es schaffst, DICH als das Opfer hinzustellen."

    -> kross_kein_mitleid

+ "Wieso denkst du das?"

    "Keine Ahnung", murmelt er, "Lässt mich nicht gerade gut aussehen, die Geschichte, oder?"
    
    ++ "Du kannst dir nur selber vergeben."
    
        "Ja, vielleicht. Wenn das so leicht wäre.
    
    ++ "Glaubst du, dass du Strafe verdienst?"

        "Für das, was wir getan haben? Auf jeden Fall. Wobei ... bestraft hat man uns ja. Und wahrscheinlich ist das auch richtig so, ja." Er schweigt lange. "Danke, Hannah", sagt er schließlich, "Du hast mich an etwas Wichtiges erinnert. Ich habe es verdient, unsichtbar zu sein. Hat keinen Sinn, sich drüber zu ärgern. Es ist meine gerechte Strafe. Kann echt helfen, das zu wissen. Und ich wusste es schon mal, ich hatte es nur vergessen." Du spürst seine Hand an deiner Schulter. "Danke", sagt er nochmal "<>

-
Jedenfalls: jetzt weißt du Bescheid, was die Geschichte angeht.
-> kross_bescheid_geschichte


=== kross_kein_mitleid
    Er gibt ein kurzes Schnauben von dich. "Manchmal erinnerst du mich an Jirka. Er konnte auch so sein. Eiskalt."

    * "Suchst du jetzt wieder die Schuld bei anderen statt bei dir?"
    
        Er flucht laut. "Du bist wirklich ein ...", er seufzt, "Na gut, mach weiter. Erzähl mir, was für ein Monster ich bin. Ich schätze, das habe ich verdient."
    
        ** "Und noch mehr Selbstmitleid."
            Er lacht freudlos.
        
        ** "Wenn du es so siehst."
            Er lacht freudlos.
            
        ** Schweige.
    
        --
        "Wie auch immer. Das war die Geschichte. Jetzt weißt du Bescheid."
        -> kross_bescheid_geschichte
        
    * "Erzähl mir mehr über Jirka."
        "Jirka. Ein gerissener Hurensohn. Vielleicht wusste er genau, was er da tut. Vielleicht hatte er einfach nur Glück. Jedenfalls hat das Ritual geklappt. Zumindest scheint mir das die naheliegendste Erklärung. Auch wenn ich vorher nicht an Dämonen und so Scheiß glauben wollte. Aber wir sind eben unsichtbar geworden.
    -> kross_uns_weiter_gesc

=== kross_bescheid_geschichte

* "Das war aber noch nicht die ganze Geschichte."
    "Ja. Aber der Rest ist sowieso ...

* "Warum bist du unsichtbar?"
    "Hmmmja. Das habe ich noch nicht erzählt, was?

-
<> Wir sind eben unsichtbar geworden.

-> kross_uns_weiter_gesc


=== kross_uns_weiter_gesc

<> Wir alle. Bis auf Jirka."

+ "Benson, Penelope und du."
    "Ja. Benson, Penelope und ich. Penelope hat es wahrscheinlich am meisten abgefuckt.

+ "Und dann?"
    "Puuuh. Das Leben als Unsichtbarer ist nicht schön, das kannst du mir glauben.

-

Du kannst dir nicht mal was anziehen. Es hilft nichts. Es dauert ein paar Minuten und dann werden die Kleider auch unsichtbar. Einmal hab ich mir Schminke ins Gesicht geschmiert, um mich zu erinnern, wie ich aussehe. Aber das war nicht mein Gesicht. Nur so'n weißer Clown. Und nach nicht mal fünf Minuten ist die Schminke verschwunden und dann war da nichts mehr. Nur noch so Pampe im Gesicht, ich hab sie gefühlt, aber gesehen hat man nichts mehr und dann hatte ich auf einmal so einen paranoiden Schub und dachte, die Schminke geht jetzt nie mehr weg, die bleibt Teil von meinem Gesicht, diese rotzige, pampige Gefühl im Gesicht und ich hab gerieben wie ein Irrer und dann war sie weg, natürlich, und im Spiegel hat man gar nichts mehr gesehen. Weißt du, was das für ein Gefühl ist, wenn man sich im Spiegel nicht sieht? Eigentlich nichts Besonderes. Fühlt sich irgendwie nach Nichts an. Das ist ja das Komische.

Tschuldigung, ich laber hier rum, dabei interessiert dich das gar nicht. Das mit Jirka, darauf wollte ich eigentlich raus. Jirka ist ganz normal geblieben. Dachten wir. Aber er hatte irgendwas anderes an sich. Vorher war er dieser Wichtigtuer gewesen. So ein Streber, den keiner ernst genommen hat. Aber plötzlich haben ihn die Leute ernst genommen. Ich habe ihn dann noch ein paar Mal getroffen. Er hat angefangen, Karriere zu machen. Anfangs lief es gar nicht so gut in der Partei, aber er hat sich Stück für Stück hochgearbeitet. Ich hab ihn im Fernsehen gesehen. Er hatte irgendwie jedes Jahr mehr Persönlichkeit. Seine Reden sind besser geworden. Die Umfragen sind besser geworden. Benson hat immer gesagt, dass der Fluch ihm so etwas wie eine Aura gegeben hat. Keine Ahnung, ob das stimmt.

Naja, so lief es jedenfalls. Er hat Karriere gemacht und wir sind in der Unsichtbarkeit verschwunden. Wir konnten uns ja nicht zu erkennen geben. Wer weiß, was für Experimente die mit uns angestellt hätten. Wir sind einmal quer durch Europa. In irgendwelchen alten, verlassenen Häusern haben wir gelebt. Wo uns niemand gestört hat. Wir hatten ja nur uns drei. Ich glaube, Benson war bis zum Schluss in Penny verknallt. Ich fand sie auch scharf. Auch nachdem man sie nicht mehr sehen konnte. Schöne Stimme. Aber sie wollte ni. Keinen von uns beiden. Vielleicht war sie immer in Jirka verknallt gewesen. Weiß der Geier."

* "Ihr wart aber nicht nur zu dritt."
    "Hmmmja. Das war unser anderer Fehler. Das ist aber auf Penelopes Mist gewachsen. Und darüber rede ich nicht."

* "Wenn Jirka Němec magische Überzeugungkräfte besitzt, kann ihn dann überhaupt jemand aufhalten?"
    -- (kross_jir_aufhalten)
    "Auf seinem Weg zum Präsidentschaftsamt, meinst du? Keine Ahnung. Die Rechten sind auch ziemlich stark, glaube ich." Er schnalzt mit der Zunge. "Jedenfalls ist er nicht unsterblich. Selbst wenn er Präsident wird und selbst wenn er's schafft, die Verfassung so zu ändern, dass er immer Präsident bleibt: irgendwann verreckt er auch."

    ** "Das hört sich aber nicht gerade rosig an."
        "Ja, vielleicht. Ich interessiere mich nicht wirklich für Politik. Jedenfalls weißt du jetzt Bescheid über unseren Präsidenten in spe. Keine schöne Geschichte. Aber du wolltest es ja unbedingt wissen. Ich werde mich jetzt verdrücken.
        -> kross_out_loop
        
    ** "Was meinst du, was ich mit dem Dolch tun sollte?"
        -> kross_dolch_zerst

-

* "Wenn Jirka Němec magische Überzeugungkräfte besitzt, kann ihn dann überhaupt jemand aufhalten?"
    -> kross_jir_aufhalten
        

* "Was meinst du, was ich mit dem Dolch tun sollte?"
    -> kross_dolch_zerst
    
 

=== kross_dolch_zerst
    "Das, was ich hätte tun sollen, wenn ich nicht so ein Vollidiot wäre", sagt er, "Zerstör ihn. Das Scheißding hat schon genug Scheiße angerichtet. Ich werde mich jetzt verdrücken.
    -> kross_out_loop

=== kross_out_loop

Vielleicht haue ich ganz ab aus Prag. Hab diese Stadt schon immer gehasst. Weiß der Geier, warum ich überhaupt hierhin zurückgekommen bin. Hat mich gefreut, dich kennengelernt zu haben, Hannah Kavka. Du bist wirklich was eigenes, weißt du das?"

+ "Ja."
    Er lacht rau. "Na, dann."

+ Schweige.

    "Na, dann", sagt er.

-

Du spitzt die Ohren und hörst sich entfernende Schritte.

+ weiter
-

Und damit endet die Geschichte, schätze ich.

+ weiter
-

Du hast den Dolch.

+ weiter
-

Kassandra hat überlebt.

+ weiter
-

Und ein blonder Kerl in einem Krankenhauskittel hält dir eine Pistole unter die Nase.

+ weiter
-

Oh, verdammt.

+ weiter
-

Es ist noch lange nicht vorbei.


<h1>Ende von Episode 1<h1>

-> END



